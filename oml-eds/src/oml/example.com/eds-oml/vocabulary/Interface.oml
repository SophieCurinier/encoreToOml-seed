vocabulary <http://example.com/eds-oml/vocabulary/Interface#> as Interface {

	extends <http://www.w3.org/2001/XMLSchema#> as xsd

	extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs

	extends <http://example.com/eds-oml/vocabulary/Aspects#> as aspects

	extends <http://example.com/eds-oml/vocabulary/Field#> as field

	@rdfs:comment "Defines connections and subnetwork interfaces"
	concept Interface < aspects:IdentifiedThing [
		//restricts abstract to true
		//restricts level to max 4
	]
	
	concept InterfaceSet
	
	relation Element[
		from InterfaceSet
		to Interface
	]
	
	// Base Interface // 
	
	@rdfs:comment "Identifies one existing interface definition"
	concept BaseInterfaceSet
	
	relation BaseInterfaceSetInterface[
		from BaseInterfaceSet
		to Interface
	]
	
	relation InterfaceBaseInterfaceSet[
		from Interface
		to BaseInterfaceSet
		functional
	]
	
	
	// Generic Type //
	
	concept GenericTypeSet
	
	@rdfs:comment "Specifies a generic type to be used by the interface"
	concept GenericType < aspects:IdentifiedThing
	
	relation GenericTypeElement[
		from GenericTypeSet
		to GenericType
	]
	
	relation InterfaceGenericTypeSet[
		from Interface
		to GenericTypeSet
		functional
	]
	
	// Parameter //
	
	concept ParameterSet
	
	concept Parameter < field:ExternalField
	
	relation ParameterElement[
		from ParameterSet
		to Parameter
	]
	
	relation InterfaceParameterSet[
		from Interface
		to ParameterSet
		functional
	]
	
	// Command //
	
	concept CommandSet
	
	concept Command < aspects:IdentifiedThing
	
	relation CommandElement[
		from CommandSet
		to Command
	]
	
	relation InterfaceCommandSet[
		from Interface
		to CommandSet
		functional
	]
	
	// Argument //
	
	concept Argument < field:ExternalField

	relation ArgumentOnCommand [
		from Command
		to Argument
	]

	@rdfs:comment "If true, indicates the interface may not be used directly by a component"
	scalar property abstract [
		domain Interface
		range xsd:boolean
		functional
	]

	@rdfs:comment "Indicates the system level at which it operates"
	scalar property level [
		domain Interface
		range xsd:float
	]


	concept Mapping < field:Field

	relation MappingOnGenericType [
		from GenericType
		to Mapping
	]

}